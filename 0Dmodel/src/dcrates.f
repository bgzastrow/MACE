CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  SUBROUTINE TO CALCULATE THE RATE COEFFICIENTS (K)  C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

      SUBROUTINE RATES
      DOUBLE PRECISION Y(466),K(10000),ALF(10000,5),BET(10000,5),
     *   GAM(10000,5),TLOWER(10000,5),TUPPER(10000,5),TOTAL(10),X(10),
     *   RAD,AUV,H2COL,TEMP,ZETA,ALBEDO,ACCR,HNR,X_G,A_G,
     *   RADIUS,PI,MH,MU,KB,DTMIN,DT,AV,EARG,MASS(468),SH
      INTEGER RTYPE(10000),NTR(10000),NREAC,ICO,ICOR,TR,I,J,IFREEZE
      COMMON/BL1/ K,X,TOTAL,GR,DN,ACCR,HNR,IFREEZE
      COMMON/BL10/ MASS,SH
      COMMON/BL3/ Y,X_G,A_G,TEMP,AV,ZETA,ALBEDO,RAD
      COMMON/BL4/ ALF,BET,GAM,TLOWER,TUPPER,RTYPE,NTR,NREAC,ICOR
C  PHYSICAL CONSTANTS
      DATA PI,MH,MU,KB/3.1415927,1.6605E-24,2.2,1.3807E-16/

C  TEMPERATURE
C      TEMP = 10.0

C  H-ATOM ACCRETION RATE
C      ACCR = 0.3*PI*(A_G**2.0)*DN*X_G*(8.0*KB*TEMP/(PI*MH))**0.5


C  *****CALCULATE REACTION RATES****************************************

      DO J=1,NREAC

C  DETERMINE TEMPERATURE RANGE FOR RATE COEFFICIENTS

C  INITIALISE TEMPERATURE RANGE INDEX
         TR = 1
C  IF MULTIPLE TEMP RANGES, FIND THE CLOSEST MATCH TO CURRENT TEMP.
         IF(NTR(J).GT.1) THEN
            DTMIN = 1.0E20
            DO I = 1,NTR(J)
               IF(TEMP.GE.TLOWER(J,I).AND.TEMP.LT.TUPPER(J,I)) THEN
                  TR = I
                  GO TO 201
               ELSE
                  DT = ABS(TLOWER(J,I)-TEMP)
                  IF(ABS(TUPPER(J,I)-TEMP).LT.DT) THEN
                     DT = ABS(TUPPER(J,I)-TEMP)
                  END IF
                  IF(DT.LT.DTMIN) THEN
                     DTMIN = DT
                     TR = I
                  END IF
               END IF
            END DO
         END IF


C  COMPUTE RATE COEFFICIENTS BASED ON REACTION TYPE

C  COSMIC RAY PARTICLE RATE
 201  IF(RTYPE(J).EQ.1) THEN
      K(J) = ALF(J,TR)*ZETA

C  COSMIC RAY PHOTO-RATE
      ELSE IF(RTYPE(J).EQ.2) THEN
      K(J) = ZETA*ALF(J,TR)*((TEMP/300.0)**BET(J,TR))*GAM(J,TR)
     *   /(1.0-ALBEDO)

C  PHOTO-REACTION RATE 
      ELSE IF(RTYPE(J).EQ.3) THEN
      K(J)=RAD*ALF(J,TR)*EARG(-GAM(J,TR)*AV)

C  DEFAULT BINARY REACTION RATE
      ELSE
      K(J) = ALF(J,TR)*((TEMP/300.0)**BET(J,TR))*EARG(-GAM(J,TR)/TEMP)
      END IF
C
      END DO


      RETURN
      END
